requires 2.0.10

%ph{
#include <stdio.h>
#define KEYFILE_GENERAL "general"

%}

class StuffKeeper:Data:Tag from G:Object 
{
    private GKeyFile *data = {NULL};
    private char * path = {NULL} destroywith g_free;


    /**
     * Signals 
     */
    signal last NONE (NONE)
        void
        tag_changed(self)
        {
            gint id = self_get_id(self);
            printf("Signal my tag change : %i\n", id); 
        }

    public
        StuffKeeperDataTag*
        new_from_file (const gchar *file)
        {
            Self *obj = GET_NEW;
            obj->_priv->data = g_key_file_new();
            obj->_priv->path = g_strdup(file);
            g_key_file_load_from_file(obj->_priv->data, obj->_priv->path, G_KEY_FILE_KEEP_COMMENTS, NULL);

            return obj;
        }

    public
        StuffKeeperDataTag*
        new_with_id (const gchar *path,gint id)
        {
            Self *obj = GET_NEW;
            obj->_priv->path = g_strdup_printf("%s%c%i", path, G_DIR_SEPARATOR,id);
            obj->_priv->data = g_key_file_new();
            g_key_file_set_integer(obj->_priv->data, KEYFILE_GENERAL,"id", id);
            printf("Creating tag: %i\n", id);
            self_save_yourself(obj);
            return obj;
        }

    /* Create new StuffKeeperDataBackend */
    public 
        StuffKeeperDataTag *
        new (const gchar *path)
        {

            Self *obj = GET_NEW;
            gint id = g_random_int();
            obj->_priv->path = g_strdup_printf("%s%c%i", path, G_DIR_SEPARATOR,id);
            obj->_priv->data = g_key_file_new();
            g_key_file_set_integer(obj->_priv->data, KEYFILE_GENERAL,"id", id);
            printf("Creating tag: %i\n", id);
            self_save_yourself(obj);
            return obj;
        }

    /**
     * ID (ro)
     */
    public
        gint
        get_id(self)
        {
            gint id = g_key_file_get_integer(self->_priv->data, KEYFILE_GENERAL, "id",NULL);
            return id;
        }

    /**
     * Title (rw)
     */
    public 
    gchar *
    get_title(self)
    {
        return g_key_file_get_string(self->_priv->data, KEYFILE_GENERAL, "title", NULL); 
    }


    public 
    void
    set_title(self, const gchar *title)
    {
        g_key_file_set_string(self->_priv->data, KEYFILE_GENERAL, "title", title);
        printf("new title: %s\n", title);
        self_tag_changed(self);
    }


















    /**
     * Functions to manage 
     */
    public
        void
        save_yourself(self)
        {
            gint id = self_get_id(self);
            printf("saving myself: %i\n",id);
            if(self->_priv->data)
            {
                char *content;
                gsize size;
                content = g_key_file_to_data(self->_priv->data,&size, NULL);
                g_file_set_contents(self->_priv->path, content, size, NULL);
                g_free(content);
            }
        }

    public
        void
        delete_yourself(self)
        {
            gint id = self_get_id(self);
            printf("Deleting myself: %i\n", id);
            g_key_file_set_integer(self->_priv->data, KEYFILE_GENERAL,"id", -1);
            g_unlink(self->_priv->path);
            g_free(self->_priv->path);
            self->_priv->path = NULL;
        }

}
